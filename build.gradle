plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.github.jk1.dependency-license-report' version '1.16'
    id 'com.gorylenko.gradle-git-properties' version '2.3.1'
}

group = 'org.codeforamerica'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    jaxb
}

repositories {
    mavenCentral()
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxb"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "src/main/resources/object-service-port.wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema,
                    package: "org.codeforamerica.shiba.esbwsdl") {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: "lines,vars,source",
                    includeantruntime: false,
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.session:spring-session-bom:2021.0.0'
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.springframework.session:spring-session-jdbc'
    implementation 'io.sentry:sentry-spring-boot-starter:4.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('org.springframework.boot:spring-boot-starter-web-services') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }

    implementation 'io.awspring.cloud:spring-cloud-aws-context:2.3.1'
    implementation 'io.awspring.cloud:spring-cloud-starter-aws:2.3.1'
    implementation 'io.awspring.cloud:spring-cloud-aws-autoconfigure:2.3.1'
    implementation 'org.postgresql:postgresql'
    implementation 'org.glassfish.jaxb:jaxb-runtime'
    implementation 'org.apache.pdfbox:pdfbox:2.0.23'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'org.jetbrains:annotations:21.0.1'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.apache.httpcomponents:httpclient'
    implementation 'io.sentry:sentry-logback:5.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.google.crypto.tink:tink:1.6.0'
    implementation 'org.flywaydb:flyway-core:7.9.2'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.mixpanel:mixpanel-java:1.5.0'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.sun.activation:jakarta.activation'
    implementation 'org.springframework:spring-aspects'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.springframework.retry:spring-retry'
    implementation 'ch.qos.logback.contrib:logback-json-classic:0.1.5'
    implementation 'ch.qos.logback.contrib:logback-jackson:0.1.5'
    implementation 'ch.qos.logback:logback-core:1.2.0'
    implementation 'ch.qos.logback:logback-classic:1.2.0'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.6'

    implementation 'org.springframework.ws:spring-ws-core'
    implementation(files(genJaxb.classesDir).builtBy(genJaxb))
    jaxb "com.sun.xml.bind:jaxb-xjc:2.3.4"

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.seleniumhq.selenium:selenium-java'
    testImplementation 'de.redsix:pdfcompare:1.1.58'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    testImplementation 'com.h2database:h2'
    testImplementation 'org.awaitility:awaitility'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'
    testImplementation 'org.mockito:mockito-inline'
    testImplementation 'io.github.bonigarcia:webdrivermanager:4.4.3'
    testImplementation 'org.springframework.ws:spring-ws-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.percy:percy-java-selenium:1.0.0'
}

compileJava{
    dependsOn(generateGitProperties)
    dependsOn(genJaxb)
    inputs.files(processResources)
}

def unitTest = tasks.register("unitTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.excludeTags 'db', 'journey', 'pdf', 'ccap', 'validation', 'document'
        }
}

def journeyTest = tasks.register("journeyTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags 'journey'
        }
}

def pdfTest = tasks.register("pdfTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags 'pdf'
        }
}

def dbTest = tasks.register("dbTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags 'db'
        }
}

def ccapTest = tasks.register("ccapTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags 'ccap'
        }
}

def validationTest = tasks.register("validationTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags 'validation'
        }
}


def documentTest = tasks.register("documentTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags 'document'
        }
}

test {
    useJUnitPlatform()
    dependsOn(checkLicense)
}

tasks.withType(Test).configureEach {
    Test task ->
        task.doFirst {
            def env = new File('.env')
            if (env.exists()) {
                file('.env').readLines().each() {
                    def (key, value) = it.split('=', 2)
                    environment(key, value)
                }
            }
            file('.env.test').readLines().each() {
                def (key, value) = it.split('=', 2)
                environment(key, value)
            }
        }
        task.jvmArgs '-XX:+ShowCodeDetailsInExceptionMessages'
        task.maxParallelForks(Runtime.runtime.availableProcessors().intdiv(2) ?: 1)
        task.testLogging {
            exceptionFormat = 'full'
            events "failed", "passed", "skipped"
            showStackTraces true
            showCauses true
            showExceptions true
        }
}

licenseReport {
    allowedLicensesFile = new File("$projectDir/allowed-licenses.json")
}

bootRun {
    jvmArgs = ["-XX:+ShowCodeDetailsInExceptionMessages"]
}

jar {
    enabled = false
}