plugins {
    id("org.springframework.boot") version "3.2.3"
    id("io.spring.dependency-management") version "1.1.4"
    id("java")
    id("com.github.jk1.dependency-license-report") version "2.6"
    id("com.gorylenko.gradle-git-properties") version "2.4.1"
}

group = "org.codeforamerica"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = "17"

tasks.withType(Jar){
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    jaxb
}

repositories {
    mavenCentral()
    maven { 
    	url "https://repo.spring.io/milestone" 
    }
}

task genJaxbFilenet {
    ext.sourcesDir = "${buildDir}/generated-sources/jaxbFilenet"
    ext.classesDir = "${buildDir}/classes/jaxb"
    ext.schema = "src/main/resources/FileNetService.wsdl"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask",
                    classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema,
                    package: "org.codeforamerica.shiba.filenetwsdl") {
                arg(value: "-wsdl")
                arg(value: "-extension")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.8, target: 1.8, debug: true,
                    debugLevel: "lines,vars,source",
                    includeantruntime: false,
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
            duplicatesStrategy = DuplicatesStrategy.EXCLUDE
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.session:spring-session-bom:2021.2.3")
    }
}

dependencies {
    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    // jaxb WS libraries -->
    implementation(files(genJaxbFilenet.classesDir).builtBy(genJaxbFilenet))
    jaxb(
           "org.glassfish.jaxb:jaxb-runtime",
           "org.glassfish.jaxb:jaxb-xjc",
           "org.glassfish.jaxb:jaxb-jxc"
    )
   
    implementation("com.sun.xml.bind:jaxb-impl:4.0.5")
    implementation("jakarta.xml.bind:jakarta.xml.bind-api-parent:4.0.2")
    implementation("jakarta.activation:jakarta.activation-api:2.1.3")
    implementation("jakarta.xml.bind:jakarta.xml.bind-api:4.0.2")
    implementation("jakarta.mail:jakarta.mail-api:2.1.3")
    implementation("org.apache.commons:commons-email:1.6.0")
    implementation("org.apache.chemistry.opencmis:chemistry-opencmis-client-api:1.1.0")
    implementation("org.apache.chemistry.opencmis:chemistry-opencmis-client-impl:1.1.0")
    // end of jaxb WS libraries
    
    implementation("org.apache.commons:commons-lang3:3.14.0")
    implementation("org.apache.commons:commons-text:1.11.0")
    implementation("org.apache.httpcomponents.core5:httpcore5:5.2.4")
    implementation("org.apache.httpcomponents.client5:httpclient5:5.3.1")
    implementation("org.apache.httpcomponents.client5:httpclient5-fluent:5.3.1")
    implementation("org.apache.pdfbox:pdfbox:3.0.2")

    implementation("org.springframework:spring-aspects")
    implementation("org.springframework.retry:spring-retry")
    implementation("org.springframework.ws:spring-ws-core")
    implementation("org.springframework.session:spring-session-core:3.2.2")
    implementation("org.springframework.session:spring-session-jdbc:3.2.2")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    implementation("org.springframework.boot:spring-boot-starter-web:3.2.3")
    implementation("org.springframework.boot:spring-boot-starter-data-jdbc")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-oauth2-client")
    implementation("org.springframework.boot:spring-boot-starter-oauth2-resource-server:3.2.3")
    //webflux is needed even though it is reactive, not servlet. Used by MailGun and SmartyStreet
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-web-services:3.2.3") {
    	exclude(group: "org.springframework.boot", module: "spring-boot-starter-tomcat")
    }
    
     //logging -->
    implementation("org.slf4j:slf4j-api:2.0.12")
    implementation("io.sentry:sentry-spring-boot-starter-jakarta:7.6.0")
    implementation("io.sentry:sentry-logback:7.6.0")
    implementation("net.logstash.logback:logstash-logback-encoder:7.4")
    implementation("ch.qos.logback.contrib:logback-json-classic:0.1.5")
    implementation("ch.qos.logback.contrib:logback-jackson:0.1.5")
    implementation("ch.qos.logback:logback-classic:1.5.3")
    implementation("ch.qos.logback:logback-core:1.5.3")
    implementation("ch.qos.logback:logback-parent:1.5.3")
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation("com.google.crypto.tink:tink:1.12.0")
    implementation("com.azure:azure-storage-blob:12.25.2")
    implementation("org.postgresql:postgresql")
    implementation("org.jetbrains:annotations:24.1.0")
    implementation("commons-validator:commons-validator:1.8.0")
    implementation("org.flywaydb:flyway-core:10.10.0")
    runtimeOnly("org.flywaydb:flyway-database-postgresql:10.10.0")
    implementation("com.mixpanel:mixpanel-java:1.5.2")
    implementation("net.javacrumbs.shedlock:shedlock-spring:5.12.0")
    implementation("net.javacrumbs.shedlock:shedlock-provider-jdbc-template:5.12.0")
    implementation("net.coobird:thumbnailator:0.4.20")
    implementation("mobi.openddr.client:openddr-classifier-client:1.4.1")
    implementation("mobi.openddr:openddr-data:1.34")
    implementation("com.google.guava:guava:33.1.0-jre")

    testCompileOnly("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    testImplementation("org.seleniumhq.selenium:selenium-java")
    testImplementation("de.redsix:pdfcompare:1.2.2")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        exclude group: "com.vaadin.external.google", module: "android-json"
    }
    testImplementation("com.h2database:h2:2.2.224")
    testImplementation("org.awaitility:awaitility")
    testImplementation("com.github.tomakehurst:wiremock-standalone:3.0.1")
    testImplementation("org.mockito:mockito-inline:5.2.0")
    testImplementation("io.github.bonigarcia:webdrivermanager:5.7.0")
    testImplementation("org.springframework.ws:spring-ws-test")
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("io.percy:percy-java-selenium:2.0.5")
    testImplementation("com.deque.html.axe-core:selenium:4.8.2")
    testImplementation("org.jsoup:jsoup:1.17.2")
}

compileJava {
    dependsOn(generateGitProperties)
    dependsOn(genJaxbFilenet)
    inputs.files(processResources)
}

def unitTest = tasks.register("unitTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.excludeTags("db", "documentUploadJourney", "laterDocsJourney", "fullFlowJourney", "minimumFlowJourney", "framework", "pdf", "ccap", "validation", "a11y")
        }
}

def documentUploadJourneyTest = tasks.register("documentUploadJourneyTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("documentUploadJourney")
        }
}

def fullFlowJourneyTest = tasks.register("fullFlowJourneyTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("fullFlowJourney")
        }
}

def laterDocsJourneyTest = tasks.register("laterDocsJourneyTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("laterDocsJourney")
        }
}

def healthcareRenewalJourneyTest = tasks.register("healthcareRenewalJourneyTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("healthcareRenewalJourney")
        }
}

def minimumFlowJourneyTest = tasks.register("minimumFlowJourneyTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("minimumFlowJourney")
        }
}

def frameworkTest = tasks.register("frameworkTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("framework")
        }
}

def pdfTest = tasks.register("pdfTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("pdf")
        }
}

def dbTest = tasks.register("dbTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("db")
        }
}

def ccapTest = tasks.register("ccapTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("ccap")
        }
}

def validationTest = tasks.register("validationTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("validation")
        }
}

def axeAccessibilityTest = tasks.register("accessibilityTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("a11y")
        }
}

def virusUploadTest = tasks.register("virusUploadTest", Test) {
    Test task ->
        task.useJUnitPlatform {
            JUnitPlatformOptions options ->
                options.includeTags("virusUploadJourney")
        }
}

test {
    useJUnitPlatform {
        JUnitPlatformOptions options ->
            options.excludeTags("a11y")
    }
    dependsOn(checkLicense)
}

tasks.withType(Test).configureEach {
    Test task ->
        task.doFirst {
            file(".env.test").readLines().each() {
                def (key, value) = it.split("=", 2)
                environment(key, value)
            }
        }
        task.maxParallelForks(Runtime.runtime.availableProcessors().intdiv(2) ?: 1)
        task.testLogging {
            exceptionFormat = "full"
            events = ["failed", "skipped"]
            showStackTraces = true
            showCauses = true
            showExceptions = true
            // uncomment the following line to print stdout and stderr for every test
            // showStandardStreams = true
        }
}

licenseReport {
    allowedLicensesFile = new File("$projectDir/allowed-licenses.json")
}

jar {
    enabled = false
}